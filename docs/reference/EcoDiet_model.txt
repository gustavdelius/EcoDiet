model{
  for (i in list_pred){

    for (k in list_prey[i, 1:nb_prey[i]]){

      LAMBDA[k, i] ~ dbern(eta[k, i])

      eta[k, i] ~ dbeta(o[k, i] + 1, nb_o[i] - o[k, i] + 1)

   }

    s[i] <- sum(LAMBDA[list_prey[i, 1:nb_prey[i]], i])
    is_link_identified[i] <- ifelse(s[i] == 0, 0, 1)

  }

  for (i in 1:nb_group){

    for (j in 1:nb_elem){

      mu[j, i]  ~ dnorm(0, 1.0E-6)

    }

    for (l in 1:nb_y[i]){

      y[i, l, ] ~ dmnorm(mu[, i], SIGMA_inv[, , i])

    }

    SIGMA_inv[1:nb_elem, 1:nb_elem, i] ~ dwish(ID, nb_elem)

  }

  for (j in 1:nb_elem){

    for (i in list_pred){

      for (k in list_prey[i, 1:nb_prey[i]]){

        mix_numerator[j, k, i] <- PI[k, i] * q[j, k] * (mu[j, k] + delta[j, i])
        mix_denominator[j, k, i] <- PI[k, i] * q[j, k]

      }

      ZEROS[j, i] ~ dpois(ss[j, i])
      ss[j, i] <- pow(mu[j, i] -
      sum(mix_numerator[j, list_prey[i, 1:nb_prey[i]], i]) / sum(mix_denominator[j, list_prey[i, 1:nb_prey[i]], i]),
      2) + 0.0001

      delta[j, i] <- DELTA[j] + xi[j] * theta[j, i]
      theta[j, i] ~ dnorm(0, tau_theta[j])

    }

    xi[j] ~ dnorm(0, 0.0016)
    tau_theta[j] ~ dgamma(0.5, 0.5)

  }

  for (i in list_pred){

    for (k in list_prey[i, 1:nb_prey[i]]){

      alpha[k, i] <- (1 - is_link_identified[i]) + is_link_identified[i] * LAMBDA[k, i] + 0.1

      rho[k, i] ~ dgamma(alpha[k, i], 1)
      PI[k, i] <- rho[k, i] / sum(rho[list_prey[i, 1:nb_prey[i]], i])
    }
  }
}
